group = "uk.gov.ida"

apply plugin: 'java'
apply plugin: 'application'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/allowed-repos for production builds.\n\n')
        mavenCentral()
    }
    else {
        maven { url 'https://gds.jfrog.io/artifactory/allowed-repos' }
    }
}

ext.mainclass = 'uk.gov.ida.rp.testrp.TestRpApplication'
mainClassName = ext.mainclass

distZip {
    into(project.name + '/truststores') {
        from 'src/main/resources'
        include '*.ts'
    }
    into(project.name) {
        from 'configuration'
        include 'test-rp.yml'
    }

    def versionedPath = archivePath
    version = null
    doLast {
        archivePath.renameTo(versionedPath)
    }
}

ext {
    opensaml = '3.4.3'
}

def dependencyVersions = [
        verify_utils:395,
        dropwizard:'1.3.8',
        dropwizard_jade:10528,
        verify_test_utils: '2.0.0-64',
        verify_dev_pki: '2.0.0-45',
        opensaml_version: "$opensaml",
        saml_libs_version: "$opensaml-254",
        hub_saml_version: "$opensaml-15823"
]

configurations {
    common
    dropwizard
    frontend
    verify_utils
    jade
    jwt
    verify_test_utils
    soap
    saml
    test_deps_compile
}

configurations.all {
    resolutionStrategy {
        // dropwizard-testing uses 2.2.0 but our libraries currently only work with 1.6.0.
        force "org.assertj:assertj-core:1.6.0",
              "org.glassfish.hk2:hk2-api:2.4.0-b31",
              "org.glassfish.hk2:hk2-locator:2.4.0-b31",
              "org.glassfish.hk2.external:javax.inject:2.4.0-b31"
    }
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    common 'com.google.guava:guava:19.0',
            'org.slf4j:slf4j-api:1.7.10',
            //need this to be fixed - https://github.com/Squarespace/jersey2-guice/pull/39
            'com.hubspot.dropwizard:dropwizard-guicier:1.0.0.6',
            'com.squarespace.jersey2-guice:0-jersey2-guice-spi:1.0.6',
            'javax.xml.bind:jaxb-api:2.3.1',
            'com.sun.xml.bind:jaxb-core:2.3.0.1',
            'com.sun.xml.bind:jaxb-impl:2.3.0'

    dropwizard 'io.dropwizard:dropwizard-core:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-client:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-auth:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-configuration:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-metrics-graphite:' + dependencyVersions.dropwizard

    frontend 'io.dropwizard:dropwizard-assets:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-views-freemarker:' + dependencyVersions.dropwizard,
            'io.dropwizard:dropwizard-views:' + dependencyVersions.dropwizard,
            'org.freemarker:freemarker:2.3.27-incubating',
            'org.jsoup:jsoup:1.6.1'

    verify_utils 'uk.gov.ida:common-utils:2.0.0-' + dependencyVersions.verify_utils,
            'uk.gov.ida:rest-utils:2.0.0-' + dependencyVersions.verify_utils,
            'uk.gov.ida:security-utils:2.0.0-' + dependencyVersions.verify_utils

    jade 'uk.gov.ida:dropwizard-jade:1.3.8-' + dependencyVersions.dropwizard_jade

    jwt 'com.nimbusds:nimbus-jose-jwt:6.4'

    verify_test_utils 'uk.gov.ida:common-test-utils:' + dependencyVersions.verify_test_utils

    saml    'org.opensaml:opensaml-core:' + dependencyVersions.opensaml_version,
            'org.opensaml:opensaml-saml-impl:' + dependencyVersions.opensaml_version,
            'uk.gov.ida:saml-lib:' + dependencyVersions.saml_libs_version,
            'uk.gov.ida:saml-test:' + dependencyVersions.saml_libs_version,
            'uk.gov.ida:hub-saml:' + dependencyVersions.hub_saml_version

    soap('org.apache.ws.commons:ws-commons-util:1.0.1') { transitive = false }

    testImplementation configurations.verify_test_utils,
            configurations.test_deps_compile,
            'uk.gov.ida:verify-dev-pki:' + dependencyVersions.verify_dev_pki,
            'junit:junit:4.11',
            'org.mockito:mockito-core:2.23.4',
            'io.dropwizard:dropwizard-testing:' + dependencyVersions.dropwizard,
            'org.assertj:assertj-guava:1.1.0',
            'org.assertj:assertj-joda-time:1.1.0',
            'commons-io:commons-io:2.8.0',
            'org.json:json:20210307'

    implementation configurations.common,
            configurations.dropwizard,
            configurations.verify_utils,
            configurations.frontend,
            configurations.jade,
            configurations.saml {
                exclude group: 'uk.gov.ida', module: 'dropwizard-saml'
            }

    [
            'io.dropwizard:dropwizard-testing:' + dependencyVersions.dropwizard,
            'com.thoughtworks.inproctester:jerseytester-htmlunit:1.0.3',
            'com.thoughtworks.inproctester:jerseytester-webdriver:1.0.3',
    ].each { dep ->
        test_deps_compile(dep) { transitive = false }
    }

    testImplementation "com.github.tomakehurst:wiremock-standalone:2.23.2"
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integration-test/java'
        }
        resources {
            srcDir 'src/integration-test/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

task intTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
}

tasks.check.dependsOn(intTest)
